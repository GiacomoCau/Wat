;;; Control-related Tests

(defSuite fibers

  (defTest fiber.1
    (fiber 1 2 (eq? 1 1))
    #t)

  (defTest fiber.2
    (begin
      (def yield-record (fiber 1 2 (+ (fiberYield (eq? 1 1)) 3)))
      (assert#t (type? yield-record YieldRecord))
      (assert#t (eq? #t (getKey yield-record 'value)))
      (assert#t (eq? 33 (fiberResume yield-record 30))))
    #inert)

  (defTest fiber.begin.1
    (runFiber
     (\ ()
       (begin
         (fiberYield 1)
         (fiberYield 2)
         3)))
    '(1 2 3))

  (defTest fiber.begin.2
    (runFiberWithValues
     (\ ()
       (begin
         (fiberYield 1)
         (fiberYield 2)))
     '(#inert 3))
    '(1 2 3))

  (defTest fiber.vau.1
    (runFiber
     (\ ()
       ((vau () #ignore
          (fiberYield 1)
          (fiberYield 2)
          3))))
    '(1 2 3))

  (defTest fiber.vau.2
    (runFiberWithValues
     (\ ()
       ((vau () #ignore
          (fiberYield 1)
          (fiberYield 2))))
     '(#inert 3))
    '(1 2 3))

  (defTest fiber.\.1
    (runFiber
     (\ ()
       ((\ ()
          (fiberYield 1)
          (fiberYield 2)
          3))))
    '(1 2 3))

  (defTest fiber.\.2
    (runFiberWithValues
     (\ ()
       ((\ ()
          (fiberYield 1)
          (fiberYield 2))))
     '(#inert 3))
    '(1 2 3))

  (defTest fiber.def.1
    (runFiber
     (\ ()
       (def (x y) (list (fiberYield 1) (fiberYield 2)))
       (list x y)))
    '(1 2 (#inert #inert)))

  (defTest fiber.def.2
    (runFiberWithValues
     (\ ()
       (def (x y) (list (fiberYield 1) (fiberYield 2)))
       (list x y))
     '(3 4))
    '(1 2 (3 4)))

  (defTest fiber.if.1
    (runFiber
     (\ ()
       (if #t (fiberYield 1) (fiberYield 2))))
    '(1 #inert))

  (defTest fiber.if.2
    (runFiberWithValues
     (\ ()
       (if #t (fiberYield 1) (fiberYield 2)))
     '(3))
    '(1 3))

  (defTest fiber.if.3
    (runFiber
     (\ ()
       (if #f (fiberYield 1) (fiberYield 2))))
    '(2 #inert))

  (defTest fiber.if.4
    (runFiberWithValues
     (\ ()
       (if #f (fiberYield 1) (fiberYield 2)))
     '(3))
    '(2 3))

  (defTest fiber.if.5
    (runFiberWithValues
     (\ ()
       (if (fiberYield 1) (fiberYield 2) (fiberYield 3)))
     '(#t 4))
    '(1 2 4))

  (defTest fiber.if.6
    (runFiberWithValues
     (\ ()
       (if (fiberYield 1) (fiberYield 2) (fiberYield 3)))
     '(#f 4))
    '(1 3 4))

  (defTest fiber.if.7
    (runFiberWithValues
     (\ ()
       (if (begin (fiberYield 1) (fiberYield 2))
           (begin (fiberYield 3) (fiberYield 3.5))
           (begin (fiberYield 4) (fiberYield 4.5))))
     '(#inert #t #inert 5))
    '(1 2 3 3.5 5))

  (defTest fiber.if.8
    (runFiberWithValues
     (\ ()
       (if (begin (fiberYield 1) (fiberYield 2))
           (begin (fiberYield 3) (fiberYield 3.5))
           (begin (fiberYield 4) (fiberYield 4.5))))
     '(#inert #f #inert 5))
    '(1 2 4 4.5 5))

  (defTest fiber.loop.1
    (runFiber
     (\ ()
       (def ct 1)
       (def env (theEnv))
       (block exit
         (loop
          (if (eq? ct 5)
              (returnFrom exit ct)
              (begin
                (fiberYield ct)
                (fiberYield (- ct))
                (set env ct (+ ct 1))))))))
    '(1 -1 2 -2 3 -3 4 -4 5))

  (defTest fiber.block.1
    (runFiber
     (\ ()
       (block b
         (fiberYield 1)
         (fiberYield 2)
         3)))
    '(1 2 3))

  (defTest fiber.block.2
    (runFiberWithValues
     (\ ()
       (block b
         (fiberYield 1)
         (fiberYield 2)))
     '(#inert 3))
    '(1 2 3))

  (defTest fiber.block.3
    (runFiber
     (\ ()
       (block b
         (fiberYield 1)
         (fiberYield 2)
         (returnFrom b 3))))
    '(1 2 3))

  (defTest fiber.block.4
    (runFiberWithValues
     (\ ()
       (block b
         (fiberYield 1)
         (returnFrom b (fiberYield 2))))
     '(#inert 3))
    '(1 2 3))

  (defTest fiber.catchTag.1
    (runFiber
     (\ ()
       (catchTag 'b
         (fiberYield 1)
         (fiberYield 2)
         3)))
    '(1 2 3))

  (defTest fiber.catchTag.2
    (runFiberWithValues
     (\ ()
       (catchTag 'b
         (fiberYield 1)
         (fiberYield 2)))
     '(#inert 3))
    '(1 2 3))

  (defTest fiber.catchTag.3
    (runFiber
     (\ ()
       (catchTag 'b
         (fiberYield 1)
         (fiberYield 2)
         (throwTag 'b 3))))
    '(1 2 3))

  (defTest fiber.catchTag.4
    (runFiberWithValues
     (\ ()
       (catchTag 'b
         (fiberYield 1)
         (throwTag 'b (fiberYield 2))))
     '(#inert 3))
    '(1 2 3))

  (defTest fiber.finally.1
    (runFiberWithValues
     (\ ()
       (finally (fiberYield 1)
         (fiberYield 2)
         (fiberYield 2.5)
         3))
     '(4 #inert #inert))
    '(1 2 2.5 4))

  (defTest fiber.finally.1'
    (runFiberWithValues
     (\ ()
       (atEnd
         (begin
           (fiberYield 2)
           (fiberYield 2.5)
           3 ) 
         (fiberYield 1) ))
     '(4 #inert #inert))
    '(1 2 2.5 4))

  (defTest fiber.finally.2
    (runFiberWithValues
     (\ ()
       (block exit
         (finally (returnFrom exit (fiberYield 1))
           (fiberYield 2)
           (fiberYield 2.5)
           3)))
     '(4 #inert #inert))
    '(1 2 2.5 4))

  (defTest fiber.finally.3
    (runFiberWithValues
     (\ ()
       (block exit
         (finally (returnFrom exit 4)
           (fiberYield 2)
           (fiberYield 2.5)
           3)))
     '(#inert #inert))
    '(2 2.5 4))

  (defTest fiber.finally.4
    (runFiberWithValues
     (\ ()
       (block exit
         (finally (begin (fiberYield 1) (fiberYield 1.5))
           (fiberYield 2)
           (fiberYield 2.5)
           3)))
     '(#inert 4 #inert #inert))
    '(1 1.5 2 2.5 4))

  (defTest fiber.progv.1
    (runFiber
     (\ ()
       (ddef *x*)
       (progv (*x*) (1)
         (fiberYield (dget *x*))
         (fiberYield (dget *x*))
         (progv (*x*) (2)
           (fiberYield (dget *x*))
           (fiberYield (dget *x*))
           3))))
    '(1 1 2 2 3))

  (defTest fiber.progv.2
    (runFiberWithValues
     (\ ()
       (ddef *x*)
       (progv (*x*) (1)
         (fiberYield (dget *x*))
         (fiberYield (dget *x*))
         (progv (*x*) (2)
           (fiberYield (dget *x*))
           (fiberYield (dget *x*)))))
     '(#inert #inert #inert 3))
    '(1 1 2 2 3))

  (defTest fiber.fun.1
    (runFiber
     (\ ()
       (list (fiberYield 1) (fiberYield 2))))
    '(1 2 (#inert #inert)))

  (defTest fiber.fun.2
    (runFiberWithValues
     (\ () (list (fiberYield 1) (fiberYield 2)))
     '(3 4))
    '(1 2 (3 4))))

;;; Basic Operator Tests

(defSuite pushPrompt

  (defTest pushPrompt.1
    (signalsError?
     (pushPrompt)
     Error :type 'match :operands# +1))

  (defTest pushPrompt.2
    (pushPrompt 'p)
    #inert)

  (defTest pushPrompt.3
    (pushPrompt 'p 1 2 (eq? 1 1))
    #t)

  (defTest pushPrompt.4
    (begin
      (block ret
        (pushPrompt 'p
          (assert#t (promptSet? 'p))
          (returnFrom ret)))
      (assert#t (not (promptSet? 'p))))
    #inert)

  (defTest pushPrompt.5
    (begin
      (assert#t (not (promptSet? 'p)))
      (assert#t (not (promptSet? 'q)))
      (pushPrompt 'p
        (assert#t (promptSet? 'p))
        (assert#t (not (promptSet? 'q)))
        (pushPrompt 'q
          (assert#t (promptSet? 'p))
          (assert#t (promptSet? 'q)))
        (assert#t (promptSet? 'p))
        (assert#t (not (promptSet? 'q))))
      (assert#t (not (promptSet? 'p)))
      (assert#t (not (promptSet? 'q))))
    #inert))

(defSuite takeSubcont

  (defTest takeSubcont.1
    (signalsError?
     (takeSubcont)
     Error :type 'match :operands# +2))

  (defTest takeSubcont.2
    (signalsError?
     (takeSubcont 'p)
     Error :type 'match :operands# +1))

  (defTest takeSubcont.3
    (signalsError?
     (takeSubcont 'p #ignore)
     Error :type 'unboundPrompt :prompt 'p))

  (defTest takeSubcont.4
    (signalsError?
     (pushPrompt 'p (takeSubcont 'q #ignore))
     Error :type 'unboundPrompt :prompt 'q))

  (defTest takeSubcont.5
    (pushPrompt 'p (takeSubcont 'p #ignore))
    #inert)

  (defTest takeSubcont.6
    (pushPrompt 'p (takeSubcont 'p #ignore (eq? 1 1)))
    #t)

  (defTest takeSubcont.7
    (pushPrompt 'p (takeSubcont 'p k (pushDelimSubcont 'p k (eq? 1 1))))
    #t))

(defSuite pushDelimSubcont

  (defTest pushDelimSubcont.1
    (signalsError?
     (pushDelimSubcont)
     Error :type 'match :operands# +2))

  (defTest pushDelimSubcont.2
    (signalsError?
     (pushDelimSubcont 'p)
     Error :type 'match :operands# +1))

  (defTest pushDelimSubcont.3
    (signalsError?
     (pushDelimSubcont 'p 12)
     Error :type 'type :datum 12))

  (defTest pushDelimSubcont.4
    (let ((k (pushPrompt 'p (+ 100 (takeSubcont 'p k k)))))
      (assert#t (eq? 102 (pushDelimSubcont 'p k (+ 1 1))))
      (assert#t (eq? 120 (pushDelimSubcont 'p k (+ 10 10)))))
    #inert)

  (defTest pushDelimSubcont.5
    (let ((k (pushPrompt 'p (pushPrompt 'q (takeSubcont 'p k k)))))
      (pushDelimSubcont 'p k
        (assert#t (promptSet? 'q))
        (assert#t (promptSet? 'p))))
    #inert))

(defSuite promptSet?

  (defTest promptSet?.1
    (signalsError?
     (promptSet?)
     Error :type 'match :operands# +1))

  (defTest promptSet?.2
    (promptSet? 'p)
    #f)

  (defTest promptSet?.3
    (pushPrompt 'p (begin (promptSet? 'p)))
    #t))

(defSuite pushSubcontBarrier

  (defTest pushSubcontBarrier.1
    (signalsError?
     (pushSubcontBarrier
      (takeSubcont 'p1 sk))
     Error :type 'unboundPrompt :prompt 'p1))

  (defTest pushSubcontBarrier.2
    (signalsError?
     (pushPrompt 'p1
       (pushSubcontBarrier
         (takeSubcont 'p1 sk)))
     Error :type 'unboundPrompt :prompt 'p1)))

(defSuite dynamics

  (defTest ddef.1
    (begin
      (ddef *x* 1)
      (ddef *y* (+ 1 1))
      (assert#t (eq? (dget *x*) 1))
      (assert#t (eq? (dget *y*) 2))
      (dlet ((*x* 3))
        (assert#t (eq? (dget *x*) 3))
        (assert#t (eq? (dget *y*) 2))
        (dlet ((*y* 4))
          (assert#t (eq? (dget *x*) 3))
          (assert#t (eq? (dget *y*) 4)))
        (assert#t (eq? (dget *x*) 3))
        (assert#t (eq? (dget *y*) 2)))
      (assert#t (eq? (dget *x*) 1))
      (assert#t (eq? (dget *y*) 2)))
    #inert)

  (defTest* ddef.redefine
    (ddef *a* (+ 1 1))
    (def old-a *a*)
    (assert#t (eq? (dget *a*) 2))
    (assert#t (eq? (dget old-a) 2))
    (ddef *a* (+ 2 2))
    (assert#t (eq? (dget *a*) 4))
    (assert#t (eq? (dget old-a) 4))
    (assert#t (== old-a *a*))
    (ddef *a*)
    (assert#t (eq? (dget *a*) #inert))
    (assert#t (eq? (dget old-a) #inert))
    (assert#t (== old-a *a*)))

  (defTest progv.1
    (begin
      (ddef *x* 1)
      (ddef *y* 2)
      (assert#t (eq? (dget *x*) 1))
      (assert#t (eq? (dget *y*) 2))
      (progv (*x*) (3)
        (assert#t (eq? (dget *x*) 3))
        (assert#t (eq? (dget *y*) 2))
        (progv (*y*) (4)
          (assert#t (eq? (dget *x*) 3))
          (assert#t (eq? (dget *y*) 4)))
        (assert#t (eq? (dget *x*) 3))
        (assert#t (eq? (dget *y*) 2)))
      (assert#t (eq? (dget *x*) 1))
      (assert#t (eq? (dget *y*) 2)))
    #inert)

  (defTest dget.1
    (begin
      (ddef *foo*)
      (assert#t (eq? (dget *foo*) #inert))
      (assert#t (type? *foo* DVar))
      (assert#t (type? *foo* Box))
      (assert#t (type? *foo* Object))
      (assert#t (subClass? DVar Box))
      (assert#t (subClass? DVar Object)))
    #inert)

  (defTest dset.1
    (begin
      (ddef *bar*)
      (dlet ((*bar* 1))
        (dset *bar* 2)
        (assert#t (eq? 2 (dget *bar*)))
        (dlet ((*bar* 3))
          (assert#t (eq? 3 (dget *bar*))))
        (assert#t (eq? 2 (dget *bar*)))
        (dset *bar* 4)
        (assert#t (eq? 4 (dget *bar*))))
      (assert#t (eq? #inert (dget *bar*)))
      #t))

  (defTest dlet*.1
    (dlet* () (+ 1 1))
    2)

  (defTest dlet*.2
    (begin
      (ddef *x* 1)
      (dlet* ((*x* 2)) (+ 1 (dget *x*))))
    3)

  (defTest dlet*.2
    (begin
      (ddef *x* 1)
      (ddef *y* 0)
      (dlet* ((*x* 2) (*y* (+ (dget *x*) 1)))
        (list (dget *x*) (dget *y*))))
    '(2 3))

  (defTest dlet-sanity-check
    (begin
      (ddef *x* 1)
      (ddef *y* 0)
      (dlet ((*x* 2) (*y* (+ (dget *x*) 1)))
        (list (dget *x*) (dget *y*))))
    '(2 2)))

(defSuite built-in-prompts

  (defTest \|Default prompt exists|
    (bound? 'coroutinePrompt (theEnv)))

  (defTest \|Root prompt exists|
    (bound? 'rootPrompt (theEnv)))

  (defTest \|Root prompt is set|
    ;; I have no idea why this works.  I mean it's the right thing,
    ;; but it's unclear how it interacts with test-util.lispx's
    ;; Mocha stuff that runs this test.
    (takeSubcont rootPrompt #ignore)
    #inert))

;;; Simple Control Operators

(defSuite loop

  (defTest loop.1
    (let ((ct 0))
      (def env (theEnv))
      (block exit
        (loop 'just-a-symbol-to-test-implicit-begin
              (if (eq? ct 10)
                  (returnFrom exit ct)
                  (set env ct (+ ct 1))))))
    10))

(defSuite catchTag/throwTag

  (defTest catchTag.1
    (signalsError?
     (catchTag)
     Error :type 'match :operands# +1))

  (defTest catchTag.2
    (catchTag 'x)
    #inert)

  (defTest catchTag.3
    (catchTag 'x 1 2 3 (eq? 1 1))
    #t)

  (defTest catchTag.4
    (catchTag 'x 1 (throwTag 'x (eq? 1 2)) 3 (eq? 1 1))
    #f)

  (defTest catchTag.5
    (catchTag 'x 1 (throwTag 'x) 3 (eq? 1 1))
    #inert)

  (defTest catchTag.6
    (catchTag 'x 1 (catchTag 'y (throwTag 'x 44)) 3 (eq? 1 1))
    44)

  (defTest catchTag.7
    (catchTag 'x 1 (catchTag 'y (throwTag 'y 2)) 3 55)
    55)

  (defTest catchTag.8
    (catchTag 'x 1 (catchTag 'y (throwTag 'y 2)))
    2)

  (defTest catchTag.9
    (catchTag 'x 1 (catchTag 'y (throwTag 'y)))
    #inert))

(defSuite block/returnFrom

  (defTest block.1
    (signalsError?
     (block)
     Error :type 'match :operands# +1))

  (defTest block.2
    (block x)
    #inert)

  (defTest block.3
    (block x 1 2 3 (eq? 1 1))
    #t)

  (defTest block.4
    (block x 1 (returnFrom x (eq? 1 2)) 3 (eq? 1 1))
    #f)

  (defTest block.5
    (block x 1 (returnFrom x) 3 (eq? 1 1))
    #inert)

  (defTest block.6
    (block x 1 (block y (returnFrom x (eq? 1 2))) 3 (eq? 1 1))
    #f)

  (defTest block.7
    (block x 1 (block y (returnFrom y 2)) 3 (eq? 1 1))
    #t)

  (defTest block.8
    (block x 1 (block y (returnFrom y 2)))
    2)

  (defTest block.9
    (block x 1 (block y (returnFrom y)))
    #inert))

(defSuite finally

  (defTest finally.1
    (signalsError?
     (finally)
     Error :type 'match :operands# +1))

  (defTest finally.2
    (finally (eq? 1 1))
    #t)

  (defTest finally.3
    (begin
      (def env (theEnv))
      (+ (finally 1 2 3 (set env x 10))
         x))
    11)

  (defTest finally.4
    (begin
      (def env (theEnv))
      (+ (block exit
           (finally (returnFrom exit 1) 2 3 (set env x 10)))
         x))
    11)

  (defTest finally.5
    (block exit
      (finally 1 2 3 (returnFrom exit 4)))
    4)

  (defTest finally.6
    (block exit
      (finally (returnFrom exit 1) 2 3 (returnFrom exit 4)))
    4))
